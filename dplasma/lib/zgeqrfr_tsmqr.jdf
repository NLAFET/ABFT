extern "C" %{
/*
 * Copyright (c) 2010-2013 The University of Tennessee and The University
 *                         of Tennessee Research Foundation. All rights
 *                         reserved.
 * Copyright (c) 2013      Inria. All rights reserved.
 * $COPYRIGHT
 *
 *
 * @precisions normal z -> s d c
 *
 */
#include "dplasma/lib/dplasmajdf.h"
#include "dplasmatypes.h"
#include "parsec/data_dist/matrix/matrix.h"

%}

descA1 [type = "parsec_tiled_matrix_dc_t*"]
descA2 [type = "parsec_tiled_matrix_dc_t*" aligned=descA1]
descV  [type = "parsec_tiled_matrix_dc_t*" aligned=descA1]
descT  [type = "parsec_tiled_matrix_dc_t*" aligned=descA1]

p_work [type = "parsec_memory_pool_t *"]

ib     [type = "int" hidden=on default = "descT->mb" ]

ztsmqr_ztsmqr(k, n)
  /* Execution space */
  k = 0 .. descV->nt-1
  n = 0 .. descA2->nt-1

  : descA2(0,n)

  RW    A1 <- (k == 0)           ? descA1(0, n) : A1 ztsmqr_ztsmqr(k-1, n)
           -> (k == descV->nt-1) ? descA1(0, n) : A1 ztsmqr_ztsmqr(k+1, n)

  RW    A2 <- (k == 0)           ? descA2(0, n) : A2 ztsmqr_ztsmqr(k-1, n)
           -> (k == descV->nt-1) ? descA2(0, n) : A2 ztsmqr_ztsmqr(k+1, n)

  READ  V  <- V read_VT(k)
  READ  T  <- T read_VT(k)                                 [type = LITTLE_T]

BODY
{
    parsec_complex64_t *lA1 = ((parsec_complex64_t*)A1) + k * descA1->nb;

    int tempm  = descA1->m - k * descA1->nb;
    int tempnn = (n == (descA2->nt-1)) ? (descA2->n - n * descA2->nb) : descA2->nb;
    int tempkk = (k == (descV->nt -1)) ? (descV->n  - k * descV->nb ) : descV->nb;
    int ldv  = BLKLDD( descV,  0 );
    int lda1 = BLKLDD( descA1, 0 );
    int lda2 = BLKLDD( descA2, 0 );

    printlog("CORE_ztsmqr(%d, %d)\n", k, n);

#if !defined(PARSEC_DRY_RUN)
    void *p_elem_W = parsec_private_memory_pop( p_work );

    CORE_ztsmqr(
        PlasmaLeft, PlasmaConjTrans,
        tempm, tempnn, descA2->m, tempnn, tempkk, ib,
        lA1, lda1,
        A2,  lda2,
        V,   ldv,
        T,   descT->mb,
        p_elem_W, descT->mb);

    parsec_private_memory_push( p_work, p_elem_W );

#endif  /* !defined(PARSEC_DRY_RUN) */
}
END

read_VT(k) [profile = off]
  k = 0 .. descV->nt-1

  : descV(0, k)

  READ V <- descV(0, k)
         -> V ztsmqr_ztsmqr(k, 0 .. descA2->nt-1)

  READ T <- descT(0, k)                           [type = LITTLE_T]
         -> T ztsmqr_ztsmqr(k, 0 .. descA2->nt-1) [type = LITTLE_T]
BODY
{
    /* nothing */
}
END

extern "C" %{

/**
 *******************************************************************************
 *
 * @ingroup dplasma_Complex64_t
 *
 *  dplasma_zgeqrfr_tsmqr_New - Generates the parsec taskpool that overwrites the general
 *  M-by-N matrix C with
 *
 *                  SIDE = 'L'     SIDE = 'R'
 *  TRANS = 'N':      Q * C          C * Q
 *  TRANS = 'C':      Q**H * C       C * Q**H
 *
 *  where Q is a unitary matrix defined as the product of k elementary
 *  reflectors
 *
 *        Q = H(1) H(2) . . . H(k)
 *
 *  as returned by dplasma_zgeqrf(). Q is of order M if side = PlasmaLeft
 *  and of order N if side = PlasmaRight.
 *
 * WARNING: The computations are not done by this call.
 *
 *******************************************************************************
 *
 * @param[in] side
 *          @arg PlasmaLeft:  apply Q or Q**H from the left;
 *          @arg PlasmaRight: apply Q or Q**H from the right.
 *
 * @param[in] trans
 *          @arg PlasmaNoTrans:   no transpose, apply Q;
 *          @arg PlasmaConjTrans: conjugate transpose, apply Q**H.
 *
 * @param[in] A
 *          Descriptor of the matrix A of size M-by-K if side == PlasmaLeft, or
 *          N-by-K if side == PlasmaRight factorized with the
 *          dplasma_zgeqrf_New() routine.
 *          On entry, the i-th column must contain the vector which
 *          defines the elementary reflector H(i), for i = 1,2,...,k, as
 *          returned by dplasma_zgeqrf_New() in the first k columns of its array
 *          argument A.
 *          If side == PlasmaLeft,  M >= K >= 0.
 *          If side == PlasmaRight, N >= K >= 0.
 *
 * @param[in] T
 *          Descriptor of the matrix T distributed exactly as the A matrix. T.mb
 *          defines the IB parameter of tile QR algorithm. This matrix must be
 *          of size A.mt * T.mb - by - A.nt * T.nb, with T.nb == A.nb.
 *          This matrix is initialized during the call to dplasma_zgeqrf_New().
 *
 * @param[in,out] C
 *          Descriptor of the M-by-N matrix C.
 *          On exit, the matrix C is overwritten by the result.
 *
 *******************************************************************************
 *
 * @return
 *          \retval The parsec taskpool which describes the operation to perform
 *                  NULL if one of the parameter is incorrect
 *
 *******************************************************************************
 *
 * @sa dplasma_zgeqrfr_tsmqr_Destruct
 *
 ******************************************************************************/
parsec_taskpool_t*
dplasma_zgeqrfr_tsmqr_New( parsec_tiled_matrix_dc_t *A1,
                           parsec_tiled_matrix_dc_t *A2,
                           parsec_tiled_matrix_dc_t *V,
                           parsec_tiled_matrix_dc_t *T,
                           void *work )
{
    parsec_taskpool_t* tp;

    tp = (parsec_taskpool_t*)parsec_zgeqrfr_tsmqr_new( A1,
                                                       A2,
                                                       V,
                                                       T,
                                                       (parsec_memory_pool_t*)work );
    /* Default type */
    dplasma_add2arena_rectangle( ((parsec_zgeqrfr_tsmqr_taskpool_t*)tp)->arenas[PARSEC_zgeqrfr_tsmqr_DEFAULT_ARENA],
                            A1->mb*A1->nb*sizeof(parsec_complex64_t),
                            PARSEC_ARENA_ALIGNMENT_SSE,
                            parsec_datatype_double_complex_t, A1->mb , A1->nb, -1);

    /* Little T */
    dplasma_add2arena_rectangle( ((parsec_zgeqrfr_tsmqr_taskpool_t*)tp)->arenas[PARSEC_zgeqrfr_tsmqr_LITTLE_T_ARENA],
                                 T->mb*T->nb*sizeof(parsec_complex64_t),
                                 PARSEC_ARENA_ALIGNMENT_SSE,
                                 parsec_datatype_double_complex_t, T->mb, T->nb, -1);

    return tp;
}

/**
 *******************************************************************************
 *
 * @ingroup dplasma_complex64_t
 *
 *  dplasma_zunmqr_Destruct - Free the data structure associated to an taskpool
 *  created with dplasma_zunmqr_New().
 *
 *******************************************************************************
 *
 * @param[in,out] taskpool
 *          On entry, the taskpool to destroy.
 *          On exit, the taskpool cannot be used anymore.
 *
 *******************************************************************************
 *
 * @sa dplasma_zunmqr_New
 * @sa dplasma_zunmqr
 *
 ******************************************************************************/
void
dplasma_zgeqrfr_tsmqr_Destruct( parsec_taskpool_t *tp )
{
    parsec_zgeqrfr_tsmqr_taskpool_t *parsec_zgeqrfr_tsmqr = (parsec_zgeqrfr_tsmqr_taskpool_t *)tp;

    parsec_matrix_del2arena( parsec_zgeqrfr_tsmqr->arenas[PARSEC_zgeqrfr_tsmqr_DEFAULT_ARENA ] );
    parsec_matrix_del2arena( parsec_zgeqrfr_tsmqr->arenas[PARSEC_zgeqrfr_tsmqr_LITTLE_T_ARENA] );

    tp->destructor(tp);
}

%}
