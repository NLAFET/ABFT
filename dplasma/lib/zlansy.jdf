extern "C" %{
/*
 *  Copyright (c) 2011-2013 The University of Tennessee and The University
 *                          of Tennessee Research Foundation.  All rights
 *                          reserved.
 *  Copyright (c) 2013      Inria. All rights reserved.
 *
 * @precisions normal z -> s d c
 *
 * This jdf returns the value of the infinity norm of a matrix A
 * where the infinity norm  of a matrix is the maximum row sum.
 *
 * This jdf is optimized for 2D-Block cyclic distributed data with a grid
 * P-by-Q.
 * The first step sums the local data of each rows.
 * The second finishes the sums on each rows. At the end of this step, all Q
 * processes belonging to a row have the same data.
 * The third step search the local maximum.
 * The fourth step combines the local maxima together. At the end all processes
 * owns the same value.
 *
 * The reductions are down by a pipeline followed by a broadcast of the results.
 *
 */
#include <math.h>
#include "dplasma/lib/dplasmajdf.h"
#include "parsec/data_dist/matrix/matrix.h"

#define my_rank_of(m, n) (((parsec_data_collection_t*)(descA))->rank_of((parsec_data_collection_t*)descA, m, n))
%}

/* Globals
 */
P            [type = "int"]
Q            [type = "int"]
PQ           [type = "int" hidden=on default="P*Q"]
ntype        [type = "PLASMA_enum"]
uplo         [type = "PLASMA_enum"]
trans        [type = "PLASMA_enum"]
descA        [type = "const parsec_tiled_matrix_dc_t*"]
Tdist        [type = "parsec_data_collection_t *"]
norm         [type = "double *"]


/**************************************************
 *
 *                    STEP 1
 *
 *  For j in [1,Q], W(m, j) = reduce( A(m, j+k*Q) )
 *
 **************************************************/
STEP1(m,n)

    // Execution space
    m = 0 .. descA->mt-1
    n = 0 .. descA->nt-1
    m1 = %{
    if ( (uplo == PlasmaLower && m < n) ||
         (uplo == PlasmaUpper && n < m) )
        return n;
    else
        return m;
    %}
    n1 = %{
    if ( (uplo == PlasmaLower && m < n) ||
         (uplo == PlasmaUpper && n < m) )
        return m;
    else
        return n;
    %}
    lr = %{ return my_rank_of( m1, n1 ); %}
    prevn = %{
        int nn = n-1;
        int r;
        while (nn > -1) {
            if ( (uplo == PlasmaLower && m  < nn) ||
                 (uplo == PlasmaUpper && nn < m ) )
                r = my_rank_of( nn, m );
            else
                r = my_rank_of( m, nn );
            if ( r == lr )
                break;
            else
                nn--;
        }
        return nn;
    %}

    nextn = %{
        int nn = n+1;
        int r;
        while (nn < descA->nt) {
            if ( (uplo == PlasmaLower && m  < nn) ||
                 (uplo == PlasmaUpper && nn < m ) )
                r = my_rank_of( nn, m );
            else
                r = my_rank_of( m, nn );
            if ( r == lr )
                break;
            else
                nn++;
        }
        return nn;
    %}

    // Parallel partitioning
    :descA(m1, n1)

    // Parameters
    READ A <-  descA(m1, n1)
    RW   W <-  ( prevn+1 > 0         ) ? W STEP1( m, prevn ) : NEW              [type = COL]
           ->  ( nextn   < descA->nt ) ? W STEP1( m, nextn ) : W STEP2( m, lr ) [type = COL]

BODY
{
    int tempmm = ( m1 == (descA->mt-1) ) ? descA->m - m1 * descA->mb : descA->mb;
    int tempnn = ( n1 == (descA->nt-1) ) ? descA->n - n1 * descA->nb : descA->nb;
    int ldam = BLKLDD( descA, m );

    printlog("zlange STEP1(%d, %d)\n"
             "\t( tempmm=%d, tempnn=%d, A(%d, %d)[%p], lda=%d, W(%d,%d)[%p])\n",
             m, n, tempmm, tempnn, m, n, A, ldam, m, n%Q, W);

#if !defined(PARSEC_DRY_RUN)
    double *dW = (double*)W;

    /*
     * Max norm
     */
    if (ntype == PlasmaMaxNorm) {
        double lnorm = 0.;
        if (!(prevn+1 > 0)) {
            *dW = 0.;
        }
        if ( n == m ) {
            if (trans == PlasmaConjTrans) {
                CORE_zlanhe(PlasmaMaxNorm, uplo, tempmm,
                            A, ldam, NULL, &lnorm);
            } else {
                CORE_zlansy(PlasmaMaxNorm, uplo, tempmm,
                            A, ldam, NULL, &lnorm);
            }
        } else {
            CORE_zlange(PlasmaMaxNorm, tempmm, tempnn,
                        A, ldam, NULL, &lnorm);
        }
        *dW = ( lnorm > *dW ) ? lnorm : *dW;
    }
    /*
     * Frobenius Norm
     */
    else if (ntype == PlasmaFrobeniusNorm) {
        if (!(prevn+1 > 0)) {
            dW[0] = 0.;
            dW[1] = 1.;
        }
        if ( n == m ) {
            if (trans == PlasmaConjTrans) {
                CORE_zhessq( uplo, tempmm, A, ldam, dW, dW+1);
            }
            else {
                CORE_zsyssq( uplo, tempmm, A, ldam, dW, dW+1);
            }
        } else {
            CORE_zgessq(tempmm, tempnn,
                        A, ldam, dW, dW+1 );
        }
    }
    /*
     * Inf and One -norms
     */
    else {
        if (!(prevn+1 > 0)) {
            memset( W, 0, descA->mb * sizeof(double) );
        }
        if ( m == n ) {
            CORE_dzasum(PlasmaRowwise, uplo,
                        tempmm, tempnn,
                        A, ldam, W);
        } else {

            /* !uplo side */
            if ( (uplo == PlasmaLower && m < n) ||
                 (uplo == PlasmaUpper && n < m) ) {
                CORE_dzasum(PlasmaColumnwise, PlasmaUpperLower,
                            tempmm, tempnn,
                            A, ldam, W);
                /* uplo side */
            } else {
                CORE_dzasum(PlasmaRowwise, PlasmaUpperLower,
                            tempmm, tempnn,
                            A, ldam, W);
            }
        }
    }
#endif /* !defined(PARSEC_DRY_RUN) */
}
END

/**************************************************
 *                    STEP 2
 *
 *  For each j, W(m, j) = reduce( W(m, 0..Q-1) )
 *
 **************************************************/
STEP2(m, n)

    // Execution space
    m = 0 .. descA->mt-1
    n = 0 .. PQ-1
    hadtile = %{
        int nn = descA->nt-1;
        int r;
        while (nn > -1) {
            if ( (uplo == PlasmaLower && m  < nn) ||
                 (uplo == PlasmaUpper && nn < m ) )
                r = my_rank_of( nn, m );
            else
                r = my_rank_of( m, nn );
            if ( r == n )
                break;
            else
                nn--;
        }
        return nn;
        %}

    // Parallel partitioning
    :Tdist(m, n)

    // Parameters
    READ A <- ( n+Q < PQ )       ? W STEP2(m, n+Q)  : NULL                     [type = COL]
    RW   W <- ((hadtile+1) == 0) ? NEW              : W STEP1(m, hadtile)      [type = COL]
           -> ( n < Q )          ? W STEP2BIS(m, n) : A STEP2(m, n-Q)          [type = COL]

BODY
{
    int tempmm = ( m == (descA->mt-1) ) ? descA->m - m * descA->mb : descA->mb;

    printlog("zlange STEP2(%d, %d)\n"
             "\t( tempmm=%d, W(%d, %d) + W(%d, %d)\n",
             m, n, tempmm, m, n-1, m, n);

#if !defined(PARSEC_DRY_RUN)
    double *dA = (double*)A;
    double *dW = (double*)W;

    if (ntype == PlasmaMaxNorm) {
        if ((hadtile+1) == 0) {
            *dW = 0.;
        }

        if(n+Q < PQ) {
            *dW = ( *dA > *dW ) ? *dA : *dW;
        }
    }
    else if (ntype == PlasmaFrobeniusNorm) {
        if ((hadtile+1) == 0) {
            dW[0] = 0.;
            dW[1] = 1.;
        }
        if(n+Q < PQ) {
            if ( dA[0] > 0. ){
                if( dW[0] < dA[0] ) {
                    dW[1] = dA[1] + (dW[1] * (( dW[0] / dA[0] ) * ( dW[0] / dA[0] )));
                    dW[0] = dA[0];
                } else {
                    dW[1] = dW[1] + (dA[1] * (( dA[0] / dW[0] ) * ( dA[0] / dW[0] )));
                }
            }
        }
    }
    else {
        if ((hadtile+1) == 0) {
            memset( W, 0, descA->mb * sizeof(double) );
        }
        if(n+Q < PQ) {
            cblas_daxpy( tempmm, 1., dA, 1, dW, 1);
        }
    }
#endif /* !defined(PARSEC_DRY_RUN) */
}
END

/**************************************************
 *                    STEP 2
 *
 *  For each j, W(m, j) = reduce( W(m, 0..Q-1) )
 *
 **************************************************/
STEP2BIS(m, n)

    // Execution space
    m = 0 .. descA->mt-1
    n = 0 .. Q-1

    // Parallel partitioning
    :Tdist(m, n)

    // Parameters
    READ A <- ( n == 0     ) ? NULL               : W STEP2BIS(m, n-1)   [type = COL]
    RW   W <- W STEP2(m, n)                                              [type = COL]
           -> ( n == (Q-1) ) ? A STEP3(m, 0..Q-1) : A STEP2BIS(m, n+1)   [type = COL]

BODY
{
    int tempmm = ( m == (descA->mt-1) ) ? descA->m - m * descA->mb : descA->mb;

    printlog("zlange STEP2(%d, %d)\n"
             "\t( tempmm=%d, W(%d, %d) + W(%d, %d)\n",
             m, n, tempmm, m, n-1, m, n);

#if !defined(PARSEC_DRY_RUN)
    if(n > 0)
    {
        double *dA = (double*)A;
        double *dW = (double*)W;

        if (ntype == PlasmaMaxNorm) {
            *dW = ( *dA > *dW ) ? *dA : *dW;
        }
        else if (ntype == PlasmaFrobeniusNorm) {
            if ( dA[0] > 0. ){
                if( dW[0] < dA[0] ) {
                    dW[1] = dA[1] + (dW[1] * (( dW[0] / dA[0] ) * ( dW[0] / dA[0] )));
                    dW[0] = dA[0];
                } else {
                    dW[1] = dW[1] + (dA[1] * (( dA[0] / dW[0] ) * ( dA[0] / dW[0] )));
                }
            }
        }
        else {
            cblas_daxpy( tempmm, 1., dA, 1, dW, 1);
        }
    }
#endif /* !defined(PARSEC_DRY_RUN) */
}
END

/**************************************************
 *
 *                    STEP3
 *
 * For m in 0..P-1, W(m, n) = max( W(m..mt[P], n ) )
 *
 **************************************************/
STEP3(m, n)

    // Execution space
    m = 0 .. descA->mt-1
    n = 0 .. Q-1

    // Parallel partitioning
    :Tdist(m, n)

    // Parameters
    READ A <- W STEP2BIS(m, Q-1)                                         [type = COL]
    RW   W <- (m < (descA->mt-P)) ? W STEP3( m+P, n ) : NEW               [type = ELT]
           -> (m < P            ) ? W STEP4( m,   n ) : W STEP3( m-P, n ) [type = ELT]

BODY
{
    int tempmm = ( m == descA->mt-1) ? descA->m - m * descA->mb  : descA->mb;

    printlog("zlange STEP3(%d, %d)\n", m, n);

#if !defined(PARSEC_DRY_RUN)
    double *dA = (double*)A;
    double *dW = (double*)W;

    if (ntype == PlasmaMaxNorm){
        if ( m < (descA->mt-P) ) {
            *dW = ( *dA > *dW ) ? *dA : *dW;
        } else {
            *dW = *dA;
        }
    }
    else if (ntype == PlasmaFrobeniusNorm) {
        if ( m >= (descA->mt-P) ) {
            dW[0] = 0.;
            dW[1] = 1.;
        }
        if( dW[0] < dA[0] ) {
            dW[1] = dA[1] + (dW[1] * (( dW[0] / dA[0] ) * ( dW[0] / dA[0] )));
            dW[0] = dA[0];
        } else {
            dW[1] = dW[1] + (dA[1] * (( dA[0] / dW[0] ) * ( dA[0] / dW[0] )));
        }
    }
    else {
        double maxval = 0;
        int i;

        for(i = 0; i < tempmm; i++, dA++)
            maxval = ( maxval > *dA ) ? maxval : *dA;

        if ( m < (descA->mt-P) ) {
            *dW = ( maxval > *dW ) ? maxval : *dW;
        } else {
            *dW = maxval;
        }
    }
#endif /* !defined(PARSEC_DRY_RUN) */
}
END

/**************************************************
 *                    STEP 4
 *
 *  For each i, W(i, n) = max( W(0..P-1, n) )
 *
 **************************************************/
STEP4(m,n)

    // Execution space
    m = 0..P-1
    n = 0..Q-1

    // Parallel partitioning
    :Tdist(m, n)

    // Parameters
    READ A <- ( m == 0 )            ? NULL                   : W STEP4(m-1, n) [type = ELT]
    RW   W <- ( m >  (descA->mt-1)) ? NEW                    : W STEP3(m,   n) [type = ELT]
           -> ( m == (P-1)        ) ? W WRITE_RES(0..P-1, n) : A STEP4(m+1, n) [type = ELT]

BODY
{
    printlog("zlange STEP4(%d, %d)\n", m, n);

    double *dA = (double*)A;
    double *dW = (double*)W;
    if (ntype == PlasmaFrobeniusNorm) {
        double  sqr;

        if ( m > (descA->mt-1)) {
            dW[0] = 0.;
            dW[1] = 1.;
        }
        if(m > 0) {
            if( dW[0] < dA[0] ) {
                sqr = dW[0] / dA[0];
                sqr = sqr * sqr;
                dW[1] = dA[1] + sqr * dW[1];
                dW[0] = dA[0];
            } else {
                sqr = dA[0] / dW[0];
                sqr = sqr * sqr;
                dW[1] = dW[1] + sqr * dA[1];
            }
        }
    } else {
        if ( m > (descA->mt-1)) {
            *dW = 0.;
        }
        if(m > 0) {
            *dW = ( *dA > *dW ) ? *dA : *dW;
        }
    }
}
END

/**************************************************
 *                      STEP5                     *
 **************************************************/
WRITE_RES(m,n)

    // Execution space
    m = 0..P-1
    n = 0..Q-1

    // Parallel partitioning
    :Tdist(m, n)

    // Parameters
    READ W <- W STEP4( P-1, n )   [type = ELT]

BODY
{
    printlog("zlange STORE RESULT(%d, %d)\n", m, n);

    double *dW = (double*)W;

    if (ntype == PlasmaFrobeniusNorm) {
        *norm = dW[0] * dplasma_dsqrt( dW[1] );
    } else {
        *norm = *dW;
    }
}
END
