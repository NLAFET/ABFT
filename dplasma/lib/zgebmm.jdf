extern "C" %{
/*
 * Copyright (c) 2010-2014 The University of Tennessee and The University
 *                         of Tennessee Research Foundation. All rights
 *                         reserved.
 * Copyright (c) 2013      Inria. All rights reserved.
 * $COPYRIGHT
 *
 * @precisions normal z -> s d c
 *
 */
#include "dplasma/lib/dplasmajdf.h"
#include "parsec/data_dist/matrix/matrix.h"

#include "parsec/private_mempool.h"
#include "dplasma/lib/butterfly_map.h"

extern void RBMM_zTOP( int mb, int nb, int lda, int off, int lvl, int N, int trans,
          PLASMA_Complex64_t *top, PLASMA_Complex64_t *btm,
          PLASMA_Complex64_t *C, PLASMA_Complex64_t *U_but_vec );
extern void RBMM_zBTM( int mb, int nb, int lda, int off, int lvl, int N, int trans,
          PLASMA_Complex64_t *top, PLASMA_Complex64_t *btm,
          PLASMA_Complex64_t *C, PLASMA_Complex64_t *U_but_vec );

%}

descA  [type = "parsec_seg_dc_t*"]
U      [type = "PLASMA_Complex64_t *"]
nt     [type = "int"]
mt     [type = "int"]
trans  [type = "int"]
pool_0 [type = "parsec_memory_pool_t *" size = "(sizeof(PLASMA_Complex64_t)*descA->A_org.mb*descA->A_org.nb)"]

%option PARSEC_ARENA_STRUT = 36

/*
 *
 */
GEBMM(i,j)
  /* Execution space */
  i = 0 .. mt/2-1
  j = 0 .. nt/2-1

  : descA(i,j)

  /* Atl: A from Top Left */
  RW  Atl <- A GEBMM_Reader(i,j) [ type = %{ return segment_to_arena_index(descA, i, j); %}]
          -> A GEBMM_Writer(i,j) [ type = %{ return segment_to_arena_index(descA, i, j); %}]

  /* Abl: A from Bottom Left */
  RW  Abl <- A GEBMM_Reader(i+mt/2,j) [ type = %{ return segment_to_arena_index(descA, i, j); %}]
          -> A GEBMM_Writer(i+mt/2,j) [ type = %{ return segment_to_arena_index(descA, i, j); %}]

  /* Atr: A from Top Right */
  RW  Atr <- A GEBMM_Reader(i,j+nt/2) [ type = %{ return segment_to_arena_index(descA, i, j); %}]
          -> A GEBMM_Writer(i,j+nt/2) [ type = %{ return segment_to_arena_index(descA, i, j); %}]

  /* Abr: A from Bottom Right */
  RW  Abr <- A GEBMM_Reader(i+mt/2,j+nt/2) [ type = %{ return segment_to_arena_index(descA, i, j); %}]
          -> A GEBMM_Writer(i+mt/2,j+nt/2) [ type = %{ return segment_to_arena_index(descA, i, j); %}]

BODY

  unsigned jj, mb_seg, nb_seg, lda, N, lvl;
  uintptr_t off_in_U, off_in_tile;
  int type, sz, type_exists, i_tile, j_tile;
  void *p_elem_tl, *p_elem_bl, *p_elem_tr, *p_elem_br;

  printlog( "GEBMM(%d, %d) (PlasmaLower, tempkn, A(%d,%d)[%p], ldak)\n",
             i, j, i, j, Atl);

  type = segment_to_type_index(&descA->seg_info, i, j);
  type_exists = type_index_to_sizes(&descA->seg_info, type, &mb_seg, &nb_seg);
  assert( type_exists ); (void)type_exists;
  lda = descA->A_org->mb; /* This is the mb of the tile */
  segment_to_tile(descA, i, j, &i_tile, &j_tile, &off_in_tile);

  /*
   * off_in_U is the beginning (vertically) of the segment as an absolute offest
   * from the top of the original matrix
   */
  off_in_U = i_tile*lda + off_in_tile%lda;

  N = descA->A_org->lm;
  lvl = descA->level;

  p_elem_tl = parsec_private_memory_pop( pool_0 );
  p_elem_bl = parsec_private_memory_pop( pool_0 );
  p_elem_tr = parsec_private_memory_pop( pool_0 );
  p_elem_br = parsec_private_memory_pop( pool_0 );

#if defined(DEBUG_BUTTERFLY)
but_debug ("-- GEBMM\n");
#endif
  RBMM_zTOP( mb_seg, nb_seg, lda, off_in_U, lvl, N, trans, Atl, Abl, p_elem_tl, U);
  RBMM_zBTM( mb_seg, nb_seg, lda, off_in_U, lvl, N, trans, Atl, Abl, p_elem_bl, U);
  RBMM_zTOP( mb_seg, nb_seg, lda, off_in_U, lvl, N, trans, Atr, Abr, p_elem_tr, U);
  RBMM_zBTM( mb_seg, nb_seg, lda, off_in_U, lvl, N, trans, Atr, Abr, p_elem_br, U);

  for(jj=0; jj<nb_seg; jj++){
      uintptr_t off = (lda*jj) * sizeof(PLASMA_Complex64_t);
      sz = mb_seg * sizeof(PLASMA_Complex64_t);
      memcpy((void *)((uintptr_t)Atl+off), (void *)((uintptr_t)p_elem_tl+off), sz);
      memcpy((void *)((uintptr_t)Abl+off), (void *)((uintptr_t)p_elem_bl+off), sz);
      memcpy((void *)((uintptr_t)Atr+off), (void *)((uintptr_t)p_elem_tr+off), sz);
      memcpy((void *)((uintptr_t)Abr+off), (void *)((uintptr_t)p_elem_br+off), sz);
  }

  parsec_private_memory_push( pool_0, p_elem_tl );
  parsec_private_memory_push( pool_0, p_elem_bl );
  parsec_private_memory_push( pool_0, p_elem_tr );
  parsec_private_memory_push( pool_0, p_elem_br );

END


/*
 *
 */
GEBMM_Reader(i,j)
  /* Execution space */
  i = 0 .. mt-1
  j = 0 .. nt-1

  : descA(i,j)

READ A <- descA(i, j)                                              [ type = %{ return segment_to_arena_index(descA, i, j); %}]
       -> ( (i<mt/2)  && (j<nt/2)  ) ? Atl GEBMM(i,j)           [ type = %{ return segment_to_arena_index(descA, i, j); %}]
       -> ( (i<mt/2)  && (j>=nt/2) ) ? Atr GEBMM(i,j-nt/2)      [ type = %{ return segment_to_arena_index(descA, i, j); %}]
       -> ( (i>=mt/2) && (j<nt/2)  ) ? Abl GEBMM(i-mt/2,j)      [ type = %{ return segment_to_arena_index(descA, i, j); %}]
       -> ( (i>=mt/2) && (j>=nt/2) ) ? Abr GEBMM(i-mt/2,j-nt/2) [ type = %{ return segment_to_arena_index(descA, i, j); %}]

BODY
  /* nothing */

  printlog( "GEBMM_Reader(%d, %d) (PlasmaLower, tempkn, A(%d,%d)[%p], ldak)\n",
             i, j, i, j, A);

#if defined(DEBUG_BUTTERFLY)
  but_debug ("R A[%d][%d]:%lf\n",i,j,*(double *)A);
#endif


END


/*
 *
 */
GEBMM_Writer(i,j)
  /* Execution space */
  i = 0 .. mt-1
  j = 0 .. nt-1

  : descA(i,j)

READ A <- ( (i<mt/2)  && (j<nt/2)  ) ? Atl GEBMM(i,j)           [ type = %{ return segment_to_arena_index(descA, i, j); %}]
       <- ( (i<mt/2)  && (j>=nt/2) ) ? Atr GEBMM(i,j-nt/2)      [ type = %{ return segment_to_arena_index(descA, i, j); %}]
       <- ( (i>=mt/2) && (j<nt/2)  ) ? Abl GEBMM(i-mt/2,j)      [ type = %{ return segment_to_arena_index(descA, i, j); %}]
       <- ( (i>=mt/2) && (j>=nt/2) ) ? Abr GEBMM(i-mt/2,j-nt/2) [ type = %{ return segment_to_arena_index(descA, i, j); %}]
       -> descA(i, j)                                              [ type = %{ return segment_to_arena_index(descA, i, j); %}]

BODY
  /* nothing */

  printlog( "GEBMM_Writer(%d, %d) (PlasmaLower, tempkn, A(%d,%d)[%p], ldak)\n",
             i, j, i, j, A);

#if defined(DEBUG_BUTTERFLY)
  but_debug ("W A[%d][%d]:%lf\n",i,j,*(double *)A);
#endif

END
